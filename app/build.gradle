apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

repositories {
  maven { url 'https://jitpack.io' }
}

apply from: 'jacoco.gradle'

android {
  compileSdkVersion 30
  defaultConfig {
    applicationId 'app.envelop'
    minSdkVersion 21
    targetSdkVersion 30
    versionCode 31
    versionName '1.3.0'
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    debug {
      applicationIdSuffix '.debug'
      testCoverageEnabled true
    }
  }
  testOptions {
    animationsDisabled = true
  }
  lintOptions {
    checkAllWarnings true
    warningsAsErrors true
    abortOnError true
    lintConfig file('lint.xml')
  }
  androidExtensions {
    experimental = true
  }
  kapt {
    correctErrorTypes = true
    arguments {
      arg("room.schemaLocation", "$projectDir/schemas".toString())
    }
  }
  packagingOptions {
    exclude 'META-INF/*'
  }
  compileOptions {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2-native-mt'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.4.2-native-mt'
  implementation 'androidx.appcompat:appcompat:1.3.0-beta01'
  implementation 'androidx.core:core-ktx:1.3.2'
  implementation 'androidx.preference:preference-ktx:1.1.1'
  implementation 'com.google.android.material:material:1.3.0-beta01'

  // Architecture Components
  implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
  implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'


  // Logging
  implementation 'com.jakewharton.timber:timber:4.7.1'

  // Serialization
  implementation 'com.google.code.gson:gson:2.8.6'

  // Blockstack
  //noinspection GradleDependency
  implementation 'com.github.blockstack:blockstack-android:0.6.0'

  // Dependency Injection
  implementation 'com.google.dagger:dagger:2.30.1'
  kapt 'com.google.dagger:dagger-compiler:2.30.1'

  // Database ORM
  def room_version = '2.2.6'
  implementation "androidx.room:room-runtime:$room_version"
  implementation "androidx.room:room-rxjava2:$room_version"
  kapt "androidx.room:room-compiler:$room_version"

  // Rx
  implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'com.trello.rxlifecycle3:rxlifecycle-android-lifecycle-kotlin:3.1.0'
  implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.1'
  def rxbinding_version = '3.1.0'
  implementation "com.jakewharton.rxbinding3:rxbinding-core:$rxbinding_version"
  implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxbinding_version"
  implementation "com.jakewharton.rxbinding3:rxbinding-material:$rxbinding_version"
  implementation "com.jakewharton.rxbinding3:rxbinding-swiperefreshlayout:$rxbinding_version"

  // Lists
  implementation ('com.airbnb.android:epoxy:4.3.1') {
    exclude group: "org.jetbrains.kotlin", module: "kotlin-android-extensions-runtime"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-parcelize-runtime"
  }
  kapt ('com.airbnb.android:epoxy-processor:4.3.1') {
    exclude group: "org.jetbrains.kotlin", module: "kotlin-android-extensions-runtime"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-parcelize-runtime"
  }

  // Encryption
  implementation 'com.madgag.spongycastle:bcpkix-jdk15on:1.58.0.0'
  implementation 'com.madgag.spongycastle:bcpg-jdk15on:1.58.0.0'

  // Testing
  testImplementation 'junit:junit:4.13.1'
  testImplementation 'org.mockito:mockito-inline:3.7.0'
  testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
  androidTestImplementation 'androidx.test:runner:1.3.0'
  androidTestImplementation 'androidx.test:rules:1.3.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.2'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
  androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
  androidTestImplementation 'org.mockito:mockito-android:3.7.0'
  androidTestImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0") {
    // Workaround for Android API < 26:
    // https://github.com/mockito/mockito/pull/2024
    exclude group: "org.mockito", module: "mockito-core"
  }
}
